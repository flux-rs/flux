use crate::{
    UserParseError,
    surface,
    lexer::{Lit, Location, Token, Delimiter, BinOpToken},
    ParseCtxt,
};
use rustc_span::{Span, Symbol, symbol::Ident};
use lalrpop_util::ParseError;


grammar(cx: &mut ParseCtxt<'_>);

pub Generics: surface::Generics = {
    <lo:@L> <params:Comma<GenericParam>> <hi:@R> => {
        surface::Generics {
            params,
            predicates: vec![],
            span: cx.map_span(lo, hi),
        }
    }
}

GenericsWithAngleBrackets: surface::Generics = {
    "<" <Generics> ">" => <>,
    <lo:@L> <hi:@R> => {
        surface::Generics {
            params: vec![],
            predicates: vec![],
            span: cx.map_span(lo, hi),
        }
    }
}

GenericParam: surface::GenericParam = {
    <name:Ident> => surface::GenericParam {
        name,
        kind: surface::GenericParamKind::Type,
        node_id: cx.next_node_id(),
    },
    <name:Ident> "as" <lo:@L> <kind:Ident> <hi:@R> =>? {
        let kind = match kind.as_str() {
            "type" => surface::GenericParamKind::Type,
            "base" => surface::GenericParamKind::Base,
            "spl"  => surface::GenericParamKind::Spl,
            _ => return Err(ParseError::User { error: UserParseError::UnexpectedToken(lo, hi) })
        };
        Ok(surface::GenericParam { name, kind, node_id: cx.next_node_id() })
    },
    "refine" <name:Ident> ":" <sort:Sort> => {
        surface::GenericParam {
            name,
            kind: surface::GenericParamKind::Refine{ sort },
            node_id: cx.next_node_id(),
        }
    },
}

pub TyAlias: surface::TyAlias = {
    <lo:@L>
    "type"
    <ident:Ident>
    <mut generics:GenericsWithAngleBrackets>
    <early_bound_params:("(" <RefineParams<"!">> ")")?>
    <refined_by_lo:@L>
    <index_params:("[" <RefineParams<"!">> "]")?>
    <refined_by_hi:@R>
    "="
    <ty:Ty>
    <hi:@R> => {
        generics.params.extend(
            early_bound_params
                .unwrap_or_default()
                .into_iter()
                .map(|param| surface::GenericParam {
                    name: param.name,
                    kind: surface::GenericParamKind::Refine { sort: param.sort },
                    node_id: cx.next_node_id(),
                }
            )
        );
        let refined_by = surface::RefinedBy {
            index_params: index_params.unwrap_or_default(),
            span: cx.map_span(refined_by_lo, refined_by_hi),
        };
        surface::TyAlias {
            ident,
            generics,
            refined_by,
            ty,
            node_id: cx.next_node_id(),
            span: cx.map_span(lo, hi)
        }
    }
}

pub RefinedBy: surface::RefinedBy = {
    <lo:@L>  <index_params:RefineParams<"!">> <hi:@R> => surface::RefinedBy {
        index_params,
        span: cx.map_span(lo, hi)
    }
}

pub Items: Vec<surface::Item> = {
    (<Item>)*
}

Item: surface::Item = {
    <Qualifier> => surface::Item::Qualifier(<>),
    <FuncDef>   => surface::Item::FuncDef(<>),
    <SortDecl>  => surface::Item::SortDecl(<>),
}

Qualifier: surface::Qualifier = {
    <lo:@L>
    <local: ("local")?>
    "qualifier"
    <name:Ident>
    "(" <args:RefineParams<"!">> ")" "{" <expr:Expr> "}"
    <hi:@R> =>? {
        let global = local.is_none();
        Ok(surface::Qualifier {
            name,
            args,
            expr,
            global,
            span: cx.map_span(lo, hi),
            node_id: cx.next_node_id(),
        })
    }
}

FuncDef: surface::FuncDef = {
    "fn" <name:Ident> <vars:SortVars?> "(" <args:RefineParams<"!">> ")" "->" <output:Sort> "{" <body:Expr> "}" => {
        surface::FuncDef {
            name,
            sort_vars: vars.unwrap_or_default(),
            args,
            output,
            body: Some(body),
            node_id: cx.next_node_id(),
        }
    },
    "fn" <name:Ident> <vars:SortVars?> "(" <args:RefineParams<"!">> ")" "->" <output:Sort> ";" => {
        surface::FuncDef {
            name,
            sort_vars: vars.unwrap_or_default(),
            args,
            output,
            body: None,
            node_id: cx.next_node_id()
        }
    }
}

SortVars: Vec<Ident> = {
    "<" <vars:Comma<Ident>> ">" => vars,
}


SortDecl: surface::SortDecl = {
    "opaque" "sort" <name:Ident> ";" => {
        surface::SortDecl { name }
    }
}

pub QualNames: surface::QualNames = {
    <names:Comma<Ident>> => surface::QualNames { names }
}

RefineParams<T>: Vec<surface::RefineParam> = Comma<RefineParam<T>>;

RefineParam<T>: surface::RefineParam = {
    <lo:@L>  <name:Ident> ":" <sort:Sort> <hi:@R> => surface::RefineParam {
        name,
        sort,
        span: cx.map_span(lo, hi),
        node_id: cx.next_node_id(),
    },
    <name:Ident> if T == "?" => surface::RefineParam {
        name,
        sort: surface::Sort::Infer,
        span: name.span,
        node_id: cx.next_node_id(),
    },
};

BaseSort: surface::BaseSort = {
    "bitvec" "<" <lo:@L> <lit:Lit> <hi:@R> ">" =>? {
        if let Ok(width) = lit.symbol.as_str().parse::<usize>() {
            Ok(surface::BaseSort::BitVec(width))
        } else {
            Err(ParseError::User { error: UserParseError::UnexpectedToken(lo, hi) })
        }
    },
    <ctor: Ident> "<" <lo:@L> <args:Comma<BaseSort>> <hi:@R> ">" => {
        surface::BaseSort::App(ctor, args, cx.next_node_id())
    },
    <sort: Ident> => surface::BaseSort::Ident(sort, cx.next_node_id()),
}

Sort: surface::Sort = {
    <base: BaseSort> => surface::Sort::Base(base),
    "(" <inputs:Comma<BaseSort>> ")" "->" <output:BaseSort> => surface::Sort::Func { <> },
    <input:BaseSort> "->" <output:BaseSort>                 => surface::Sort::Func { inputs: vec![input], output }
}

pub TraitAssocPredicate: surface::TraitAssocPredicate = {
    <lo:@L> <name:Ident> ":" <sort:Sort> <hi:@R> => {
        surface::TraitAssocPredicate {
            name,
            sort,
            span: cx.map_span(lo, hi),
            node_id: cx.next_node_id(),
        }
    }
}

pub ImplAssocPredicate: surface::ImplAssocPredicate = {
    <lo:@L> <name:Ident> "=" "|"<params:RefineParams<"!">> "|" "{" <body:Expr> "}" <hi:@R> => {
        surface::ImplAssocPredicate {
            name,
            params,
            body,
            span: cx.map_span(lo, hi),
            node_id: cx.next_node_id(),
        }
    }
}

pub FnSig: surface::FnSig = {
    <lo:@L>
    <asyncness:Async>
    "fn"
    <mut generics:GenericsWithAngleBrackets>
    "(" <args:Args> ")"
    <ret_lo:@L> <ret_hi:@R>
    <returns:("->" <Ty>)?>
    <requires:("requires" <Expr>)?>
    <ensures:("ensures" <Ensures>)?>
    <predicates:("where" <Predicates>)?>
    <hi:@R>
    => {
        let ensures = ensures.unwrap_or_default();
        let returns = if let Some(ty) = returns {
            surface::FnRetTy::Ty(ty)
        } else {
            surface::FnRetTy::Default(cx.map_span(ret_lo, ret_hi))
        };
        generics.predicates = predicates.unwrap_or_default();
        let output = surface::FnOutput {
            returns,
            ensures,
            node_id: cx.next_node_id(),
        };
        surface::FnSig {
            asyncness,
            generics,
            args,
            requires,
            output,
            span: cx.map_span(lo, hi),
            node_id: cx.next_node_id(),
        }
    },

}

Async: surface::Async = {
    <lo:@L> <asyncness:"async"?> <hi:@R> => {
        if asyncness.is_some() {
            surface::Async::Yes { node_id: cx.next_node_id(), span: cx.map_span(lo, hi) }
        } else {
            surface::Async::No
        }
    }

}

pub Variant: surface::VariantDef = {
    <lo:@L> <tys:Fields?> <ret:VariantRet?> <hi:@R> => {
        let fields = match tys {
            Some(fields) => fields,
            None => vec![],
        };
        surface::VariantDef { fields, ret, node_id: cx.next_node_id(), span: cx.map_span(lo, hi) }
    }
}

VariantRet: surface::VariantRet = {
    <path:Path> "[" <indices: Indices> "]" => {
        surface::VariantRet { <> }
    },
    <path:Path> <lo:@L> <hi:@R> => {
        let indices = surface::Indices { indices: vec![], span: cx.map_span(lo, hi) };
        surface::VariantRet { path, indices }
    }
}

Fields: Vec<surface::Ty> = {
    "(" <Comma<Ty>> ")" "->",
    "{" <Comma<Ty>> "}" "->",
}

Args    = <Comma<Arg>>;
Ensures    = <Comma<Constraint>>;
Predicates = <Comma<WhereBoundPredicate>>;

Constraint: surface::Constraint = {
    <ident:Ident> ":" <ty:Ty> => surface::Constraint::Type(ident, ty, cx.next_node_id()),
    <expr:Expr> => surface::Constraint::Pred(expr),
}


WhereBoundPredicate: surface::WhereBoundPredicate = {
    <lo:@L> <bounded_ty:Ty> ":" <bounds:GenericBounds>  <hi:@R> => {
        surface::WhereBoundPredicate {
            span: cx.map_span(lo, hi),
            bounded_ty,
            bounds,
        }
    }
}

GenericBounds: surface::GenericBounds = {
    <bound:Path> => vec![surface::TraitRef { path: bound } ]
}

Arg: surface::Arg = {
    <bind:Ident> ":" "&" "strg" <ty:Ty>              => surface::Arg::StrgRef(<>, cx.next_node_id()),
    <bind:Ident> ":" <path:Path> "{" <pred:Expr> "}" => surface::Arg::Constr(<>, cx.next_node_id()),
    <bind:Ident> ":" <ty:Ty>                         => surface::Arg::Ty(Some(bind), ty, cx.next_node_id()),
    <ty:Ty>                                          => surface::Arg::Ty(None, <>, cx.next_node_id()),
}

Pred: surface::Pred = {
     <lo:@L> <kind:PredKind> <hi:@L> => surface::Pred {
        kind,
        span: cx.map_span(lo, hi)
    }
}

PredKind: surface::PredKind = {
    <expr:Expr> => surface::PredKind::Expr(expr),
    "<" <arg:GenericArg> "as" <trait_id:Path> ">" "::" <name:Ident> <refine_args:RefineArgs> => {
        let alias_pred =
        surface::AliasPred {
            trait_id,
            name,
            generic_args: vec![arg],
        };
        surface::PredKind::Alias(alias_pred, refine_args)
    }
}

pub Ty: surface::Ty = {
    <lo:@L> <kind:TyKind> <hi:@L> => surface::Ty {
        kind,
        node_id: cx.next_node_id(),
        span: cx.map_span(lo, hi)
    }
}

TyKind: surface::TyKind = {
    <bty:BaseTy>                                      => surface::TyKind::Base(<>),
    <bty:BaseTy> "[" <indices:Indices> "]"            => surface::TyKind::Indexed { <> },
    <bty:BaseTy> "{" <bind:Ident> ":" <pred:Pred> "}" => surface::TyKind::Exists { <> },
    "{" <ty:Ty> "|" <pred:Pred> "}"                   => surface::TyKind::Constr(pred, Box::new(ty)),
    "{" <params:Comma1<RefineParam<"?">>> "." <ty:Ty> <pred:("|" <Expr>)?> "}" => {
        surface::TyKind::GeneralExists { params, ty: Box::new(ty), pred }
    },
    "(" <tys:Comma<Ty>> ")"         => surface::TyKind::Tuple(tys),

    "&" <ty:Ty>                     => surface::TyKind::Ref(surface::Mutability::Not, Box::new(ty)),
    "&" "mut" <ty:Ty>               => surface::TyKind::Ref(surface::Mutability::Mut, Box::new(ty)),

    "[" <ty:Ty> ";" <lo:@L> <lit:Lit> <hi:@R> "]" =>? {
        let span = cx.map_span(lo, hi);
        if let surface::LitKind::Integer = lit.kind {
            if let Ok(val) = lit.symbol.as_str().parse::<usize>() {
                return Ok(surface::TyKind::Array(Box::new(ty), surface::ArrayLen { val, span }));
            }
        }
        Err(ParseError::User { error: UserParseError::UnexpectedToken(lo, hi) })
    },

    "impl" <bounds:GenericBounds> => surface::TyKind::ImplTrait(cx.next_node_id(), bounds),
}

#[inline]
BaseTy: surface::BaseTy = {
    <lo:@L> <kind:BaseTyKind> <hi:@L> => surface::BaseTy {
        kind,
        span: cx.map_span(lo, hi)
    }
}

#[inline]
BaseTyKind: surface::BaseTyKind = {
    <path:Path> => surface::BaseTyKind::Path(path),
    "[" <ty:Ty> "]" => surface::BaseTyKind::Slice(Box::new(ty)),
}

GenericArgTys: Vec<surface::Ty> = {
    "<" <Comma<Ty>> ">",
    "<" <Comma<Ty>> ">(?=>)",
}

GenericArgs: Vec<surface::GenericArg> = {
    "<" <Comma<GenericArg>> ">",
    "<" <Comma<GenericArg>> ">(?=>)",
}

GenericArg: surface::GenericArg = {
    <bind:Ident> "=" <ty:Ty> => surface::GenericArg {
        kind: surface::GenericArgKind::Constraint(bind, ty),
        node_id: cx.next_node_id(),
    },
    <ty:Ty> => surface::GenericArg {
        kind: surface::GenericArgKind::Type(ty),
        node_id: cx.next_node_id()
    },
}


RefineArgs: Vec<surface::RefineArg> = {
   "(" <Comma<RefineArg>> ")"
}

Path: surface::Path = {
    <lo:@L> <segments:Sep1<"::", Ident>> <generics:GenericArgs?> <refine:RefineArgs?> <hi:@R> => {
        surface::Path {
            segments,
            generics: generics.unwrap_or_default(),
            refine: refine.unwrap_or_default(),
            span: cx.map_span(lo, hi),
            node_id: cx.next_node_id(),
        }
    }
}

Indices: surface::Indices = {
    <lo:@L> <indices:Comma<RefineArg>> <hi:@R> => surface::Indices { indices, span: cx.map_span(lo, hi) }
};

RefineArg: surface::RefineArg = {
    <lo:@L> "@" <bind:Ident> <hi:@R> => surface::RefineArg::Bind(bind, surface::BindKind::At, cx.map_span(lo, hi), cx.next_node_id()),
    <lo:@L> "#" <bind:Ident> <hi:@R> => surface::RefineArg::Bind(bind, surface::BindKind::Pound, cx.map_span(lo, hi), cx.next_node_id()),
    <Expr>                           => surface::RefineArg::Expr(<>),
    <lo:@L> "|"<params:RefineParams<"?">> "|" <body:Expr> <hi:@R> => {
        surface::RefineArg::Abs(params, body, cx.map_span(lo, hi), cx.next_node_id())
    }
};

pub Expr = Level1;

Level1 = NonAssoc<BinOp1, Level2>;  // <=>
Level2 = LeftAssoc<BinOp2, Level3>; // =>
Level3 = LeftAssoc<BinOp3, Level4>; // ||
Level4 = LeftAssoc<BinOp4, Level5>; // &&
Level5 = NonAssoc<BinOp5, Level6>;  // ==, !=, >=, <=
Level6 = LeftAssoc<BinOp6, Level7>; // >>, <<
Level7 = LeftAssoc<BinOp7, Level8>; // +, -
Level8 = LeftAssoc<BinOp8, Level9>; // *, %, /
Level9 = {
    <lo:@L> <op:UnOp> <e:Level10> <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::UnaryOp(op, Box::new(e)),
            node_id: cx.next_node_id(),
            span: cx.map_span(lo, hi),
        }
    },
    <Level10>
}
Level10: surface::Expr = {
    <lo:@L> "if" <p:Level1> "{" <e1:Level1> "}" <e2:ElseIf> <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::IfThenElse(Box::new([p, e1, e2])),
            node_id: cx.next_node_id(),
            span: cx.map_span(lo, hi),
        }
    },
    <lo:@L> <lit:Lit> <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::Literal(lit),
            node_id: cx.next_node_id(),
            span: cx.map_span(lo, hi),
        }
    },
    <lo:@L> <qpath:QPathExpr> "." <fld:Ident> <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::Dot(qpath, fld),
            node_id: cx.next_node_id(),
            span: cx.map_span(lo, hi),
        }
    },
    <lo:@L> <f:Ident> "(" <args:Comma<Level1>> ")" <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::App(f, args),
            node_id: cx.next_node_id(),
            span: cx.map_span(lo, hi),
        }
    },
    <lo:@L> <qpath:QPathExpr> <hi:@R> => surface::Expr {
        kind: surface::ExprKind::QPath(qpath),
        node_id: cx.next_node_id(),
        span: cx.map_span(lo, hi),
    },
    "(" <Level1> ")"
}

ElseIf: surface::Expr = {
    "else" <lo:@L> "if" <p:Level1> "{" <e1:Level1> "}" <e2:ElseIf> <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::IfThenElse(Box::new([p, e1, e2])),
            node_id: cx.next_node_id(),
            span: cx.map_span(lo, hi),
        }
    },
    "else" "{" <Level1> "}"
}

QPathExpr: surface::QPathExpr = {
    <lo:@L> <segments:Sep1<"::", Ident>> <hi:@R> => surface::QPathExpr {
        segments,
        node_id: cx.next_node_id(),
        span: cx.map_span(lo, hi),
    }
}

NonAssoc<Op, NextLevel>: surface::Expr = {
    <lo:@L> <e1:NextLevel> <op:Op> <e2:NextLevel> <hi:@R> => surface::Expr {
        kind: surface::ExprKind::BinaryOp(op, Box::new([e1, e2])),
        node_id: cx.next_node_id(),
        span: cx.map_span(lo, hi)
    },
    NextLevel
}
LeftAssoc<Op, NextLevel>: surface::Expr = {
    <lo:@L> <e1:LeftAssoc<Op, NextLevel>> <op:Op> <e2:NextLevel> <hi:@R> =>
        surface::Expr {
            kind: surface::ExprKind::BinaryOp(
                op,
                Box::new([e1, e2])
            ),
            node_id: cx.next_node_id(),
            span: cx.map_span(lo, hi),
        },
    NextLevel
}

BinOp1: surface::BinOp = {
    "<=>" => surface::BinOp::Iff
}

BinOp2: surface::BinOp = {
    "=>" => surface::BinOp::Imp
}

BinOp3: surface::BinOp = {
    "||" => surface::BinOp::Or,
}

BinOp4: surface::BinOp = {
    "&&" => surface::BinOp::And,
}

BinOp5: surface::BinOp = {
    "==" => surface::BinOp::Eq,
    "!=" => surface::BinOp::Ne,
    ">" => surface::BinOp::Gt,
    ">=" => surface::BinOp::Ge,
    "<" => surface::BinOp::Lt,
    "<=" => surface::BinOp::Le,
}

BinOp6: surface::BinOp = {
    // TODO(nilehmann) return right shift operator when we support it
    <lo:@L> ">(?=>)" ">" <hi:@R> =>? {
        Err(ParseError::User { error: UserParseError::UnexpectedToken(lo, hi) })
    }
}

BinOp7: surface::BinOp = {
    "+" => surface::BinOp::Add,
    "-" => surface::BinOp::Sub,
}

BinOp8: surface::BinOp = {
    "*" => surface::BinOp::Mul,
    "%" => surface::BinOp::Mod,
    "/" => surface::BinOp::Div,
}

UnOp: surface::UnOp = {
    "!" => surface::UnOp::Not,
    "-" => surface::UnOp::Neg,
}

Lit: surface::Lit = "literal";

Ident: surface::Ident = {
    <lo:@L> <name:"ident"> <hi:@R> => {
        surface::Ident { name, span: cx.map_span(lo, hi) }
    }
}

Sep<S, T>: Vec<T> = {
    <v:(<T> S)*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
}

Sep1<S, T>: Vec<T> = {
    <v:(<T> S)*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
}

Comma<T> = Sep<",", T>;
Comma1<T> = Sep1<",", T>;

Binding<A, B>: (A, B) = <A> ":" <B>;

extern {
    type Location = Location;
    type Error = UserParseError;

    enum Token {
        "mut" => Token::Mut,
        "strg" => Token::Strg,
        "requires" => Token::Requires,
        "ensures" => Token::Ensures,
        "where" => Token::Where,
        "impl" => Token::Impl,
        "qualifier" => Token::Qualifier,
        "sort" => Token::Sort,
        "opaque" => Token::Opaque,
        "local" => Token::Local,
        "bitvec" => Token::BitVec,
        "literal" => Token::Literal(<Lit>),
        "ident"   => Token::Ident(<Symbol>),
        "fn" => Token::Fn,
        "async" => Token::Async,
        "type" => Token::Type,
        "ref" => Token::Ref,
        "refine" => Token::Refine,
        "as" => Token::As,
        "@"  => Token::At,
        "#"  => Token::Pound,
        "==" => Token::EqEq,
        "!=" => Token::Ne,
        "="  => Token::Eq,
        "&&" => Token::AndAnd,
        "||" => Token::OrOr,
        "=>" => Token::FatArrow,
        "<=>" => Token::Iff,
        "+"  => Token::Plus,
        "-"  => Token::Minus,
        "/"  => Token::Slash,
        "!"  => Token::Not,
        "*"  => Token::Star,
        "|"  => Token::Caret,
        "("  => Token::OpenDelim(Delimiter::Parenthesis),
        ")"  => Token::CloseDelim(Delimiter::Parenthesis),
        "{"  => Token::OpenDelim(Delimiter::Brace),
        "}"  => Token::CloseDelim(Delimiter::Brace),
        "["  => Token::OpenDelim(Delimiter::Bracket),
        "]"  => Token::CloseDelim(Delimiter::Bracket),
        "<"  => Token::Lt,
        "<=" => Token::Le,
        ">"  => Token::Gt,
        ">(?=>)"  => Token::GtFollowedByGt,
        ">=" => Token::Ge,
        ":"  => Token::Colon,
        "."  => Token::Dot,
        ";"  => Token::Semi,
        ","  => Token::Comma,
        "->" => Token::RArrow,
        "&"  => Token::And,
        "%"  => Token::Percent,
        "if"   => Token::If,
        "else" => Token::Else,
        "::" => Token::ModSep,
    }
}
