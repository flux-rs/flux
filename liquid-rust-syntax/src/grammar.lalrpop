use crate::{
    UserParseError,
    ast,
    lexer::{Lit, Location, Token, DelimToken, BinOpToken}
};
use rustc_span::{Span, Symbol};
use lalrpop_util::ParseError;

grammar<F>(mk_span: &F) where F: Fn(Location, Location) -> Span;

pub FnSig: ast::FnSig = {
    <lo:@L>
    "fn"
    <generics:Generics>
    "(" <requires:(<Env> ";")?> <args:Comma<Ty>> ")"
    "->"
    <ret:Ty> <ensures:(";" <Env>)?>
    <hi:@R>
    => {
        let requires = requires.unwrap_or_else(|| vec![]);
        let ensures = ensures.unwrap_or_else(|| vec![]);
        ast::FnSig { generics, requires, args, ret, ensures, span: mk_span(lo, hi) }
    }
}

Ty: ast::Ty = {
    <lo:@L> <kind:TyKind> <hi:@L> => ast::Ty {
        kind,
        span: mk_span(lo, hi)
    }
}

TyKind: ast::TyKind = {
    <path:Path>                                        => ast::TyKind::BaseTy(<>),
    <path:Path> "@" <refine:Refinement>                => ast::TyKind::RefineTy { <> },
    <path:Path> "{" <bind:Ident> ":" <pred:Level1> "}" => ast::TyKind::Exists { <> },
    "ref" "<" <ident:Ident> ">"                        => ast::TyKind::MutRef(<>),
}

Generics: ast::Generics = {
    <lo:@L> "<" <params:Comma<GenericParam>> ">" <hi:@R> => ast::Generics { params, span: mk_span(lo, hi) },
    <lo:@L> <hi:@R> => ast::Generics::empty(mk_span(lo, hi))
}

Env = <Comma<(<Ident> ":" <Ty>)>>;

GenericParam: ast::GenericParam = {
    <name:Ident> ":" <sort:Ident> "{" <pred:Level1> "}" => ast::GenericParam { name, sort, pred: Some(pred) },
    <name:Ident> ":" <sort:Ident>                       => ast::GenericParam { name, sort, pred: None },
}

Path: ast::Path = {
    <lo:@L> <ident:Ident> <args:GenericArgs?> <hi:@R> => ast::Path { ident, args, span: mk_span(lo, hi) }
}

GenericArgs: Vec<ast::Ty> = {
    "<" <Comma<Ty>> ">"
}

Refinement: ast::Expr = {
    <lo:@L> <lit:Lit> <hi:@R> => {
        ast::Expr {
            kind: ast::ExprKind::Literal(lit),
            span: mk_span(lo, hi),
        }
    },
    <lo:@L> <var:Ident> <hi:@R> => ast::Expr {
        kind: ast::ExprKind::Var(var),
        span: mk_span(lo, hi),
    },
    "{" <Level1> "}" => <>
};

Level1 = NonAssoc<BinOp1, Level2>;  // <=>
Level2 = LeftAssoc<BinOp2, Level3>; // =>
Level3 = LeftAssoc<BinOp3, Level4>; // ||
Level4 = LeftAssoc<BinOp4, Level5>; // &&
Level5 = NonAssoc<BinOp5, Level6>;  // ==, >=, <=, ...
Level6 = LeftAssoc<BinOp6, Level7>; // +, -, ...
Level7: ast::Expr = {
    <lo:@L> <lit:Lit> <hi:@R> => {
        ast::Expr {
            kind: ast::ExprKind::Literal(lit),
            span: mk_span(lo, hi),
        }
    },
    <lo:@L> <var:Ident> <hi:@R> => ast::Expr {
        kind: ast::ExprKind::Var(var),
        span: mk_span(lo, hi),
    },
    "(" <Level1> ")" => <>
}


NonAssoc<Op, NextLevel>: ast::Expr = {
    <lo:@L> <e1:NextLevel> <op:Op> <e2:NextLevel> <hi:@R> => ast::Expr {
        kind: ast::ExprKind::BinaryOp(op, Box::new(e1), Box::new(e2)),
        span: mk_span(lo, hi)
    },
    NextLevel
}
LeftAssoc<Op, NextLevel>: ast::Expr = {
    <lo:@L> <e1:LeftAssoc<Op, NextLevel>> <op:Op> <e2:NextLevel> <hi:@R> =>
        ast::Expr {
            kind: ast::ExprKind::BinaryOp(
                op,
                Box::new(e1),
                Box::new(e2)
            ),
            span: mk_span(lo, hi),
        },
    NextLevel
}

BinOp1: ast::BinOp = {
    "<=>" => ast::BinOp::Iff
}

BinOp2: ast::BinOp = {
    "=>" => ast::BinOp::Imp
}

BinOp3: ast::BinOp = {
    "||" => ast::BinOp::Or,
}

BinOp4: ast::BinOp = {
    "&&" => ast::BinOp::And,
}

BinOp5: ast::BinOp = {
    "==" => ast::BinOp::Eq,
    ">" => ast::BinOp::Gt,
    ">=" => ast::BinOp::Ge,
    "<" => ast::BinOp::Lt,
    "<=" => ast::BinOp::Le,
}

BinOp6: ast::BinOp = {
    "+" => ast::BinOp::Add,
    "-" => ast::BinOp::Sub,
    "%" => ast::BinOp::Mod,
}

Lit: ast::Lit = {
    <lo:@L> <lit:"literal"> <hi:@R> => ast::Lit {
        kind: lit.kind,
        span: mk_span(lo, hi),
        symbol: lit.symbol
    },
}


Ident: ast::Ident = {
    <lo:@L> <name:"ident"> <hi:@R> => {
        ast::Ident { name, span: mk_span(lo, hi) }
    }
}

Sep<S, T>: Vec<T> = {
    <v:(<T> S)*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
}
Comma<T> = Sep<",", T>;
Binding<A, B>: (A, B) = <A> ":" <B>;

extern {
    type Location = Location;
    type Error = UserParseError;

    enum Token {
        "literal" => Token::Literal(<Lit>),
        "ident"   => Token::Ident(<Symbol>),
        "fn" => Token::Fn,
        "ref" => Token::Ref,
        "@"  => Token::At,
        "==" => Token::EqEq,
        "&&" => Token::AndAnd,
        "||" => Token::OrOr,
        "=>" => Token::FatArrow,
        "<=>" => Token::Iff,
        "+"  => Token::Plus,
        "-"  => Token::Minus,
        "|"  => Token::Caret,
        "("  => Token::OpenDelim(DelimToken::Paren),
        ")"  => Token::CloseDelim(DelimToken::Paren),
        "{"  => Token::OpenDelim(DelimToken::Brace),
        "}"  => Token::CloseDelim(DelimToken::Brace),
        "<"  => Token::Lt,
        "<="  => Token::Le,
        ">"  => Token::Gt,
        ">="  => Token::Ge,
        ":"  => Token::Colon,
        ";"  => Token::Semi,
        ","  => Token::Comma,
        "->" => Token::RArrow,
        "%" => Token::Percent,
    }
}
