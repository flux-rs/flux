use std::str::FromStr;
use liquid_rust_core::ast::*;
use liquid_rust_core::names::*;
use codespan::Span;
use std::iter::FromIterator;

grammar();

match {
  r"\s*" => {}, // The default whitespace skipping is disabled if an `ignore pattern` is specified
  r"//[^\n\r]*[\n\r]*" => { },                          // Skip `// comments`
  r"/\*([^\*]*\*+[^\*/])*([^\*]*\*+|[^\*])*\*/" => { }, // Skip `/* comments */`
  _,
}

pub FnDef: FnDef<Span, &'input str> =
 "fn"
 <name: FnId>
 <regions: UniversalRegions?>
 "(" <in_heap: Heap> ";" <formal: Params> ")"
 "ret" <ret: ContId> "(" <out_heap: Heap> ";" <output: OwnRef> ")"
 "=" <body: FnBody> => {
   let mut inputs = vec![];
   let mut params = vec![];
   for (local, ty) in formal {
     params.push(local);
     inputs.push(ty);
   }
   let ty = FnTy { regions: regions.unwrap_or_default(), in_heap, inputs, out_heap, output };
   FnDef { params, body: *body, ty, ret }
 };

UniversalRegions: Vec<UniversalRegion<&'input str>> =
  "<" <Comma<UniversalRegion>> ">";

FnBody: Box<FnBody<Span, &'input str>> = {
  "letcont" <defs: And<ContDef>> "in" <rest: FnBody> => Box::new(FnBody::LetCont(defs, rest)),

  "if" <discr: Place> "then" <then: FnBody> "else" <else_: FnBody>
    => Box::new(FnBody::Ite{ <> }),

  "call" <func: Place> "(" <args: Comma<Local>> ")" "ret" <ret: ContId>
    => Box::new(FnBody::Call{ <> }),

  "jump" <target: ContId> "(" <args: Local*> ")" => Box::new(FnBody::Jump{ <> }),

  <Statement> ";" <FnBody> => Box::new(FnBody::Seq(<>)),

  "abort" => Box::new(FnBody::Abort)
}

ContDef: ContDef<Span, &'input str> = {
   <name: ContId> "(" <heap: Heap> ";" <locals: Comma<Binding<Local, OwnRef>>> ";" <formal: Params> ")" "=" <body: FnBody>  => {
    let mut inputs = vec![];
    let mut params = vec![];
    for (local, ty) in formal {
      params.push(local);
      inputs.push(ty);
    }
    let ty = ContTy { heap, locals, inputs };
    ContDef { name, params, ty, body }
  }
}

Params = Comma<Binding<Local, OwnRef>>;

Statement: Statement<Span, &'input str> = {
  <lo:@L> <kind: StatementKind> <hi:@R> =>
    Statement { kind, source_info: Span::new(lo as u32, hi as u32) }
}

StatementKind: StatementKind<&'input str> = {
  "let" <Local> "=" "alloc" "(" <TypeLayout> ")" => StatementKind::Let(<>),
  <Place> ":=" <Rvalue>                          => StatementKind::Assign(<>),
  "drop" "(" <Place> ")"                         => StatementKind::Drop(<>),
}

Rvalue: Rvalue<&'input str> = {
  Operand                                                         => Rvalue::Use(<>),
  <op1: Operand> <bin_op: BinOp> <op2: Operand>                   => Rvalue::BinaryOp(bin_op, op1, op2),
  "Checked" "(" <op1: Operand> <bin_op: BinOp> <op2: Operand> ")" => Rvalue::CheckedBinaryOp(bin_op, op1, op2),
  <un_op: UnOp> <op: Operand>                                     => Rvalue::UnaryOp(un_op, op),
  "&" <p: Place>                                                  => Rvalue::Ref(BorrowKind::Shared, p),
  "&" "mut" <p: Place>                                            => Rvalue::Ref(BorrowKind::Mut, p),
}

UnOp: UnOp = {
  "!" => UnOp::Not
}

BinOp: BinOp = {
  "+"  => BinOp::Add,
  "-"  => BinOp::Sub,
  "<"  => BinOp::Lt,
  "<=" => BinOp::Le,
  "==" => BinOp::Eq,
  ">=" => BinOp::Ge,
  ">"  => BinOp::Gt,
}

Operand: Operand<&'input str> = {
  <Constant>     => Operand::Constant(<>),
  <Place>        => Operand::Copy(<>),
  "move" <Place> => Operand::Move(<>),
}

Constant: Constant = {
  "true"    => Constant::Bool(true),
  "false"   => Constant::Bool(false),
  r"[0-9]+" => Constant::Int(u128::from_str(<>).unwrap()),
  "(" ")"   => Constant::Unit
}

TypeLayout: TypeLayout = {
  r"[0-9]+"                   => TypeLayout::Block(usize::from_str(<>).unwrap()),
  "(" <Comma<TypeLayout>> ")" => TypeLayout::Tuple(<>)
}

// -------------------------------------------------------------------------------------------------
// | Types
// -------------------------------------------------------------------------------------------------

OwnRef: Location<&'input str> = "own" "(" <Location> ")" => <>;

Heap: Heap<&'input str> = Comma<Binding<Location, Ty>> => Heap::from_iter(<>);

Ty: Ty<&'input str> = {
  "own" "(" <Location> ")"                   => Ty::OwnRef(<>),
  "uninit" "(" <r"[0-9]+"> ")"               => Ty::Uninit(usize::from_str(<>).unwrap()),
  "{" <bty: BaseTy> "|" <refine: Refine> "}" => Ty::Refine(<>),
  "&" <Region> <Location>                    => Ty::Ref(BorrowKind::Shared, <>),
  "&" <Region> "shrd" <Location>             => Ty::Ref(BorrowKind::Shared, <>),
  "&" <Region> "mut" <Location>              => Ty::Ref(BorrowKind::Mut, <>),
  "(" <Comma<Binding<Field, Ty>>> ")"        => Ty::Tuple(<>)
}

BaseTy: BaseTy = {
  "int"  => BaseTy::Int,
  "bool" => BaseTy::Bool,
  "(" ")"   => BaseTy::Unit
}

Refine: Refine<&'input str> = {
  "_"        => Refine::Infer,
  ExprLevel1 => Refine::Pred(<>)
}

Region: Region<&'input str> = {
  "{" <Comma<Place>> "}" => Region::from(<>),
  "{" "_" "}"            => Region::Infer,
  UniversalRegion        => Region::Universal(<>)
}

Place: Place<&'input str> = {
  PlaceLevel1 => Place { base: <>.0, projs: <>.1 }
}

PlaceLevel1: (Local<&'input str>, Vec<Proj>) = {
  "*" <PlaceLevel1>  => {
    let (local, mut projs) = <>;
    projs.push(Proj::Deref);
    (local, projs)
  },
  PlaceLevel2
}

PlaceLevel2: (Local<&'input str>, Vec<Proj>) = {
  <p: PlaceLevel3> <path: Path> => {
      let (local, mut projs) = p;
      projs.extend(path);
      (local, projs)
  },
}

PlaceLevel3: (Local<&'input str>, Vec<Proj>) = {
  <Local>  => (<>, vec![]),
  "(" <PlaceLevel1> ")"
}

Path: Vec<Proj> = ("." <r"[0-9]+">)* => {
  // TODO: overflow
  <>.iter().map(|s| Proj::Field(usize::from_str(s).unwrap())).collect()
};

// -------------------------------------------------------------------------------------------------
// | Predicates
// -------------------------------------------------------------------------------------------------

ExprLevel1 = LeftAssoc<BinOpGroup1, ExprLevel2>;

ExprLevel2 = LeftAssoc<BinOpGroup2, ExprLevel3>;

ExprLevel3 = LeftAssoc<BinOpGroup3, ExprLevel4>;

ExprLevel4: Pred<&'input str> = {
  ConstantP            => Pred::Constant(<>),
  PlaceP               => Pred::Place(<>),
  "(" <ExprLevel1> ")" => <>
}

ConstantP: pred::Constant = {
  "true"    => pred::Constant::Bool(true),
  "false"   => pred::Constant::Bool(false),
  // TODO: overflow
  r"[0-9]+" => pred::Constant::Int(u128::from_str(<>).unwrap()),
}

PlaceP: pred::Place<&'input str> = {
  <base: Var> <projs: ProjP*> => pred::Place { <> }
}

ProjP: usize = {
  "." <r"[0-9]+"> => usize::from_str(<>).unwrap()
}

Var: pred::Var<&'input str> = {
  Location => pred::Var::Location(<>),
  Field    => pred::Var::Field(<>),
  "V"      => pred::Var::Nu
}

BinOpGroup1: pred::BinOp = {
  "&&" => pred::BinOp::And,
  "||" => pred::BinOp::Or,
};

BinOpGroup2: pred::BinOp = {
  "<"  => pred::BinOp::Lt,
  "<=" => pred::BinOp::Le,
  "==" => pred::BinOp::Eq,
  ">=" => pred::BinOp::Ge,
  ">"  => pred::BinOp::Gt,
};

BinOpGroup3: pred::BinOp = {
  "+" => pred::BinOp::Add,
  "-" => pred::BinOp::Sub
}

LeftAssoc<OpGroup, NextExprLevel>: Pred<&'input str> = {
  <lhs: LeftAssoc<OpGroup,NextExprLevel>>
  <op: OpGroup>
  <rhs: NextExprLevel>
    => Pred::BinaryOp(op, Box::new(lhs), Box::new(rhs)),
  NextExprLevel,
};

// -------------------------------------------------------------------------------------------------
// | Names
// -------------------------------------------------------------------------------------------------

Local: Local<&'input str> = r"[_a-zA-Z][a-zA-Z0-9_]*" => Local::new(<>);
Field: Field<&'input str> = r"@[a-zA-Z0-9_]+" => Field::new(&<>[1..]);
Location: Location<&'input str> = r"[_a-zA-Z][a-zA-Z0-9_]*" => Location::new(<>);
ContId: ContId<&'input str> = r"[_a-zA-Z][a-zA-Z0-9_]*" => ContId::new(<>);
FnId: FnId<&'input str> = r"[_a-zA-Z][a-zA-Z0-9_]*" => FnId::new(<>);
UniversalRegion: UniversalRegion<&'input str> = r"'[a-zA-Z0-9]" => UniversalRegion::new(&<>[1..]);

// -------------------------------------------------------------------------------------------------
// | Misc
// -------------------------------------------------------------------------------------------------

Binding<Name, T> = <Name> ":" <T>;

And<T> = Sep<"and", T>;
Comma<T> = Sep<",", T>;

Sep<S, T>: Vec<T> = {
  <v:(<T> S)*> <e:T?> => match e {
    None => v,
    Some(e) => {
      let mut v = v;
      v.push(e);
      v
    }
  }
}