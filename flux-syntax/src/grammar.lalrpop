use crate::{
    UserParseError,
    surface,
    lexer::{Lit, Location, Token, Delimiter, BinOpToken}
};
use rustc_span::{Span, Symbol, symbol::Ident};
use lalrpop_util::ParseError;

grammar<F>(mk_span: &F) where F: Fn(Location, Location) -> Span;

Generics: surface::Generics = {
    <lo:@L> "<" <params:Comma<GenericParam>> ">" <hi:@R> => {
        surface::Generics {
            params,
            span: mk_span(lo, hi),
        }
    }
}

GenericParam: surface::GenericParam = {
    <name:Ident> => surface::GenericParam { name, kind: surface::GenericParamKind::Type },
    <name:Ident> "as" <lo:@L> <kind:Ident> <hi:@R> =>? {
        let kind = match kind.as_str() {
            "type" => surface::GenericParamKind::Type,
            "base" => surface::GenericParamKind::Base,
            _ => return Err(ParseError::User { error: UserParseError::UnexpectedToken(lo, hi) })
        };
        Ok(surface::GenericParam { name, kind })
    }
}

pub TyAlias: surface::TyAlias = {
    <lo:@L>
    "type"
    <ident:Ident>
    <generics:GenericArgTys?>
    <refined_by_lo:@L>
    <early_bound_params:("(" <RefineParams<"!">> ")")?>
    <index_params:("[" <RefineParams<"!">> "]")?>
    <refined_by_hi:@R>
    "="
    <ty:Ty>
    <hi:@R> => {
        let refined_by = surface::RefinedBy {
            early_bound_params: early_bound_params.unwrap_or_default(),
            index_params: index_params.unwrap_or_default(),
            span: mk_span(refined_by_lo, refined_by_hi)
        };
        surface::TyAlias {
            ident,
            generics: generics.unwrap_or_default(),
            refined_by,
            ty,
            span: mk_span(lo, hi)
        }
    }
}

pub RefinedBy: surface::RefinedBy = {
    <lo:@L> <index_params:RefineParams<"!">> <hi:@R> => surface::RefinedBy { index_params, early_bound_params: vec![], span: mk_span(lo, hi) }
}

pub Items: Vec<surface::Item> = {
    (<Item>)*
}

Item: surface::Item = {
    <Qualifier> => surface::Item::Qualifier(<>),
    <FuncDef>   => surface::Item::FuncDef(<>),
    <SortDecl>  => surface::Item::SortDecl(<>),
}

Qualifier: surface::Qualifier = {
    <lo:@L>
    <local: ("local")?>
    "qualifier"
    <name:Ident>
    "(" <args:RefineParams<"!">> ")" "{" <expr:Expr> "}"
    <hi:@R> =>? {
        let global = local.is_none();
        Ok(surface::Qualifier { name, args, expr, global, span: mk_span(lo, hi) })
    }
}

FuncDef: surface::FuncDef = {
    "fn" <name:Ident> "(" <args:RefineParams<"!">> ")" "->" <output:Sort> "{" <body:Expr> "}" => {
        surface::FuncDef { name, args, output, body: Some(body) }
    },
    "fn" <name:Ident> "(" <args:RefineParams<"!">> ")" "->" <output:Sort> ";" => {
        surface::FuncDef { name, args, output, body: None }
    }
}

SortDecl: surface::SortDecl = {
    "opaque" "sort" <name:Ident> ";" => {
        surface::SortDecl { name }
    }
}

pub QualNames: surface::QualNames = {
    <names:Comma<Ident>> => surface::QualNames { names }
}

RefineParams<T>: Vec<surface::RefineParam> = Comma<RefineParam<T>>;

RefineParam<T>: surface::RefineParam = {
    <name:Ident> ":" <sort:Sort> => surface::RefineParam { <> },
    <name:Ident> if T == "?" => surface::RefineParam { name, sort: surface::Sort::Infer },
};

BaseSort: surface::BaseSort = {
    "bitvec" "<" <lo:@L> <lit:Lit> <hi:@R> ">" =>? {
        if let Ok(width) = lit.symbol.as_str().parse::<usize>() {
            Ok(surface::BaseSort::BitVec(width))
        } else {
            Err(ParseError::User { error: UserParseError::UnexpectedToken(lo, hi) })
        }
    },
    <ctor: Ident> "<" <lo:@L> <args:Comma<BaseSort>> <hi:@R> ">" => {
        surface::BaseSort::App(ctor, args)
    },
    <sort: Ident> => surface::BaseSort::Ident(sort),
}

Sort: surface::Sort = {
    <base: BaseSort> => surface::Sort::Base(base),
    "(" <inputs:Comma<BaseSort>> ")" "->" <output:BaseSort> => surface::Sort::Func { <> },
    <input:BaseSort> "->" <output:BaseSort>                 => surface::Sort::Func { inputs: vec![input], output }
}

pub FnSig: surface::FnSig = {
    <lo:@L>
    <asyncness:Async>
    "fn"
    <generics:Generics?>
    <params:("[" <RefineParams<"!">> "]")?>
    "(" <args:Args> ")"
    <mut returns:("->" <Ty>)?>
    <requires:("requires" <Expr>)?>
    <ensures:("ensures" <Ensures>)?>
    <predicates:("where" <Predicates>)?>
    <hi:@R>
    => {
        let ensures = ensures.unwrap_or_default();
        let params = params.unwrap_or_default();
        let predicates = predicates.unwrap_or_default();
        surface::FnSig {
            asyncness,
            generics,
            params,
            args,
            returns,
            ensures,
            requires,
            predicates,
            span: mk_span(lo, hi)
        }
    },

}

Async: surface::Async = {
    <lo:@L> <asyncness:"async"?> <hi:@R> => {
        if asyncness.is_some() {
            surface::Async::Yes { res: (), span: mk_span(lo, hi) }
        } else {
            surface::Async::No
        }
    }

}

pub Variant: surface::VariantDef = {
    <lo:@L> <tys:Fields?> <ret:VariantRet> <hi:@R> => {
        let fields = match tys {
            Some(fields) => fields,
            None => vec![],
        };
        surface::VariantDef { fields, ret, span: mk_span(lo, hi) }
    }
}

VariantRet: surface::VariantRet = {
    <path:Path> "[" <indices: Indices> "]" => {
        surface::VariantRet { <> }
    },
    <path:Path> <lo:@L> <hi:@R> => {
        let indices = surface::Indices { indices: vec![], span: mk_span(lo, hi) };
        surface::VariantRet { path, indices }
    }
}

Fields: Vec<surface::Ty> = {
    "(" <Comma<Ty>> ")" "->",
    "{" <Comma<Ty>> "}" "->",
}

Args    = <Comma<Arg>>;
Ensures = <Comma<(<Ident> ":" <Ty>)>>;
Predicates = <Comma<WhereBoundPredicate>>;

WhereBoundPredicate: surface::WhereBoundPredicate = {
    <lo:@L> <bounded_ty:Ty> ":" <bounds:Bounds>  <hi:@R> => {
        surface::WhereBoundPredicate {
            span: mk_span(lo, hi),
            bounded_ty,
            bounds,
        }
    }
}

Bounds: surface::Bounds = {
    <bound:Path> => vec![bound]
}

Arg: surface::Arg = {
    <bind:Ident> ":" "&" "strg" <ty:Ty>              => surface::Arg::StrgRef(<>),
    <bind:Ident> ":" <path:Path> "{" <pred:Expr> "}" => surface::Arg::Constr(<>),
    <bind:Ident> ":" <ty:Ty>                         => surface::Arg::Ty(Some(bind), ty),
    <ty:Ty>                                          => surface::Arg::Ty(None, <>),
}

pub Ty: surface::Ty = {
    <lo:@L> <kind:TyKind> <hi:@L> => surface::Ty {
        kind,
        span: mk_span(lo, hi)
    }
}

TyKind: surface::TyKind = {
    <bty:BaseTy>                                      => surface::TyKind::Base(<>),
    <bty:BaseTy> "[" <indices:Indices> "]"            => surface::TyKind::Indexed { <> },
    <bty:BaseTy> "{" <bind:Ident> ":" <pred:Expr> "}" => surface::TyKind::Exists { <> },
    "{" <ty:Ty> "|" <pred:Expr> "}"                   => surface::TyKind::Constr(pred, Box::new(ty)),
    "{" <params:Comma1<RefineParam<"?">>> "." <ty:Ty> <pred:("|" <Expr>)?> "}" => {
        surface::TyKind::GeneralExists { params, ty: Box::new(ty), pred }
    },
    "(" <tys:Comma<Ty>> ")"         => surface::TyKind::Tuple(tys),

    "&" <ty:Ty>                     => surface::TyKind::Ref(surface::Mutability::Not, Box::new(ty)),
    "&" "mut" <ty:Ty>               => surface::TyKind::Ref(surface::Mutability::Mut, Box::new(ty)),

    "[" <ty:Ty> ";" <lo:@L> <lit:Lit> <hi:@R> "]" =>? {
        let span = mk_span(lo, hi);
        if let surface::LitKind::Integer = lit.kind {
            if let Ok(val) = lit.symbol.as_str().parse::<usize>() {
                return Ok(surface::TyKind::Array(Box::new(ty), surface::ArrayLen { val, span }));
            }
        }
        Err(ParseError::User { error: UserParseError::UnexpectedToken(lo, hi) })
    },

    "impl" <bounds:Bounds> => surface::TyKind::ImplTrait((), bounds),
}

#[inline]
BaseTy: surface::BaseTy = {
    <lo:@L> <kind:BaseTyKind> <hi:@L> => surface::BaseTy {
        kind,
        span: mk_span(lo, hi)
    }
}

#[inline]
BaseTyKind: surface::BaseTyKind = {
    <path:Path> => surface::BaseTyKind::Path(path),
    "[" <ty:Ty> "]" => surface::BaseTyKind::Slice(Box::new(ty)),
}

GenericArgTys: Vec<surface::Ty> = {
    "<" <Comma<Ty>> ">",
    "<" <Comma<Ty>> ">(?=>)",
}

GenericArgs: Vec<surface::GenericArg> = {
    "<" <Comma<GenericArg>> ">",
    "<" <Comma<GenericArg>> ">(?=>)",
}

GenericArg: surface::GenericArg = {
    <bind:Ident> "=" <ty:Ty> => surface::GenericArg::Constraint(bind, ty),
    <ty:Ty> => surface::GenericArg::Type(ty),
}


RefineArgs: Vec<surface::RefineArg> = {
   "(" <Comma<RefineArg>> ")"
}

Path: surface::Path = {
    <lo:@L> <segments:Sep1<"::", Ident>> <generics:GenericArgs?> <refine:RefineArgs?> <hi:@R> => {
        surface::Path {
            segments,
            generics: generics.unwrap_or_default(),
            refine: refine.unwrap_or_default(),
            span: mk_span(lo, hi),
            res: (),
        }
    }
}

Indices: surface::Indices = {
    <lo:@L> <indices:Comma<RefineArg>> <hi:@R> => surface::Indices { indices, span: mk_span(lo, hi) }
};

RefineArg: surface::RefineArg = {
    <lo:@L> "@" <bind:Ident> <hi:@R> => surface::RefineArg::Bind(bind, surface::BindKind::At, mk_span(lo, hi)),
    <lo:@L> "#" <bind:Ident> <hi:@R> => surface::RefineArg::Bind(bind, surface::BindKind::Pound, mk_span(lo, hi)),
    <Expr>                           => surface::RefineArg::Expr(<>),
    <lo:@L> "|"<params:RefineParams<"?">> "|" <body:Expr> <hi:@R> => {
        surface::RefineArg::Abs(params, body, mk_span(lo, hi))
    }
};

pub Expr = Level1;

Level1 = NonAssoc<BinOp1, Level2>;  // <=>
Level2 = LeftAssoc<BinOp2, Level3>; // =>
Level3 = LeftAssoc<BinOp3, Level4>; // ||
Level4 = LeftAssoc<BinOp4, Level5>; // &&
Level5 = NonAssoc<BinOp5, Level6>;  // ==, !=, >=, <=
Level6 = LeftAssoc<BinOp6, Level7>; // >>, <<
Level7 = LeftAssoc<BinOp7, Level8>; // +, -
Level8 = LeftAssoc<BinOp8, Level9>; // *, %, /
Level9 = {
    <lo:@L> <op:UnOp> <e:Level10> <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::UnaryOp(op, Box::new(e)),
            span: mk_span(lo, hi),
        }
    },
    <Level10>
}
Level10: surface::Expr = {
    <lo:@L> "if" <p:Level1> "{" <e1:Level1> "}" <e2:ElseIf> <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::IfThenElse(Box::new([p, e1, e2])),
            span: mk_span(lo, hi),
        }
    },
    <lo:@L> <lit:Lit> <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::Literal(lit),
            span: mk_span(lo, hi),
        }
    },
    <lo:@L> <qpath:QPathExpr> "." <fld:Ident> <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::Dot(qpath, fld),
            span: mk_span(lo, hi),
        }
    },
    <lo:@L> <f:Ident> "(" <args:Comma<Level1>> ")" <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::App(f, args),
            span: mk_span(lo, hi),
        }
    },
    <lo:@L> <qpath:QPathExpr> <hi:@R> => surface::Expr {
        kind: surface::ExprKind::QPath(qpath),
        span: mk_span(lo, hi),
    },
    "(" <Level1> ")"
}

ElseIf: surface::Expr = {
    "else" <lo:@L> "if" <p:Level1> "{" <e1:Level1> "}" <e2:ElseIf> <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::IfThenElse(Box::new([p, e1, e2])),
            span: mk_span(lo, hi),
        }
    },
    "else" "{" <Level1> "}"
}

QPathExpr: surface::QPathExpr = {
    <lo:@L> <segments:Sep1<"::", Ident>> <hi:@R> => surface::QPathExpr {
        segments,
        span: mk_span(lo, hi),
    }
}

NonAssoc<Op, NextLevel>: surface::Expr = {
    <lo:@L> <e1:NextLevel> <op:Op> <e2:NextLevel> <hi:@R> => surface::Expr {
        kind: surface::ExprKind::BinaryOp(op, Box::new([e1, e2])),
        span: mk_span(lo, hi)
    },
    NextLevel
}
LeftAssoc<Op, NextLevel>: surface::Expr = {
    <lo:@L> <e1:LeftAssoc<Op, NextLevel>> <op:Op> <e2:NextLevel> <hi:@R> =>
        surface::Expr {
            kind: surface::ExprKind::BinaryOp(
                op,
                Box::new([e1, e2])
            ),
            span: mk_span(lo, hi),
        },
    NextLevel
}

BinOp1: surface::BinOp = {
    "<=>" => surface::BinOp::Iff
}

BinOp2: surface::BinOp = {
    "=>" => surface::BinOp::Imp
}

BinOp3: surface::BinOp = {
    "||" => surface::BinOp::Or,
}

BinOp4: surface::BinOp = {
    "&&" => surface::BinOp::And,
}

BinOp5: surface::BinOp = {
    "==" => surface::BinOp::Eq,
    "!=" => surface::BinOp::Ne,
    ">" => surface::BinOp::Gt,
    ">=" => surface::BinOp::Ge,
    "<" => surface::BinOp::Lt,
    "<=" => surface::BinOp::Le,
}

BinOp6: surface::BinOp = {
    // TODO(nilehmann) return right shift operator when we support it
    <lo:@L> ">(?=>)" ">" <hi:@R> =>? {
        Err(ParseError::User { error: UserParseError::UnexpectedToken(lo, hi) })
    }
}

BinOp7: surface::BinOp = {
    "+" => surface::BinOp::Add,
    "-" => surface::BinOp::Sub,
}

BinOp8: surface::BinOp = {
    "*" => surface::BinOp::Mul,
    "%" => surface::BinOp::Mod,
    "/" => surface::BinOp::Div,
}

UnOp: surface::UnOp = {
    "!" => surface::UnOp::Not,
    "-" => surface::UnOp::Neg,
}

Lit: surface::Lit = "literal";

Ident: surface::Ident = {
    <lo:@L> <name:"ident"> <hi:@R> => {
        surface::Ident { name, span: mk_span(lo, hi) }
    }
}

Sep<S, T>: Vec<T> = {
    <v:(<T> S)*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
}

Sep1<S, T>: Vec<T> = {
    <v:(<T> S)*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
}

Comma<T> = Sep<",", T>;
Comma1<T> = Sep1<",", T>;

Binding<A, B>: (A, B) = <A> ":" <B>;

extern {
    type Location = Location;
    type Error = UserParseError;

    enum Token {
        "mut" => Token::Mut,
        "strg" => Token::Strg,
        "requires" => Token::Requires,
        "ensures" => Token::Ensures,
        "where" => Token::Where,
        "impl" => Token::Impl,
        "qualifier" => Token::Qualifier,
        "sort" => Token::Sort,
        "opaque" => Token::Opaque,
        "local" => Token::Local,
        "bitvec" => Token::BitVec,
        "literal" => Token::Literal(<Lit>),
        "ident"   => Token::Ident(<Symbol>),
        "fn" => Token::Fn,
        "async" => Token::Async,
        "type" => Token::Type,
        "ref" => Token::Ref,
        "as" => Token::As,
        "@"  => Token::At,
        "#"  => Token::Pound,
        "==" => Token::EqEq,
        "!=" => Token::Ne,
        "="  => Token::Eq,
        "&&" => Token::AndAnd,
        "||" => Token::OrOr,
        "=>" => Token::FatArrow,
        "<=>" => Token::Iff,
        "+"  => Token::Plus,
        "-"  => Token::Minus,
        "/"  => Token::Slash,
        "!"  => Token::Not,
        "*"  => Token::Star,
        "|"  => Token::Caret,
        "("  => Token::OpenDelim(Delimiter::Parenthesis),
        ")"  => Token::CloseDelim(Delimiter::Parenthesis),
        "{"  => Token::OpenDelim(Delimiter::Brace),
        "}"  => Token::CloseDelim(Delimiter::Brace),
        "["  => Token::OpenDelim(Delimiter::Bracket),
        "]"  => Token::CloseDelim(Delimiter::Bracket),
        "<"  => Token::Lt,
        "<=" => Token::Le,
        ">"  => Token::Gt,
        ">(?=>)"  => Token::GtFollowedByGt,
        ">=" => Token::Ge,
        ":"  => Token::Colon,
        "."  => Token::Dot,
        ";"  => Token::Semi,
        ","  => Token::Comma,
        "->" => Token::RArrow,
        "&"  => Token::And,
        "%"  => Token::Percent,
        "if"   => Token::If,
        "else" => Token::Else,
        "::" => Token::ModSep,
    }
}
