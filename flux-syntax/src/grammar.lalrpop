use crate::{
    UserParseError,
    surface,
    lexer::{Lit, Location, Token, Delimiter, BinOpToken}
};
use rustc_span::{Span, Symbol};
use lalrpop_util::ParseError;

grammar<F>(mk_span: &F) where F: Fn(Location, Location) -> Span;

pub TyAlias: surface::TyAlias = {
    <lo:@L>
    "type"
    <ident:Ident>
    <generics:GenericArgs?>
    <refined_by_lo:@L>
    <early_bound_params:("(" <Comma<RefineParam>> ")")?>
    <index_params:("[" <Comma<RefineParam>> "]")?>
    <refined_by_hi:@R>
    "="
    <ty:Ty>
    <hi:@R> => {
        let refined_by = surface::RefinedBy {
            early_bound_params: early_bound_params.unwrap_or_default(),
            index_params: index_params.unwrap_or_default(),
            span: mk_span(refined_by_lo, refined_by_hi)
        };
        surface::TyAlias {
            ident,
            generics: generics.unwrap_or_default(),
            refined_by,
            ty,
            span: mk_span(lo, hi)
        }
    }
}

pub RefinedBy: surface::RefinedBy = {
    <lo:@L> <index_params:Comma<RefineParam>> <hi:@R> => surface::RefinedBy { index_params, early_bound_params: vec![], span: mk_span(lo, hi) }
}

pub Items: Vec<surface::Item> = {
    (<Item>)*
}

Item: surface::Item = {
    <Qualifier> => surface::Item::Qualifier(<>),
    <Defn>      => surface::Item::Defn(<>),
    <Uif>       => surface::Item::Uif(<>),
    <SortDecl>  => surface::Item::SortDecl(<>),
}

Qualifier: surface::Qualifier = {
    <lo:@L>
    <local: ("local")?>
    "qualifier"
    <name:Ident>
    "(" <args:Comma<RefineParam>> ")" "{" <expr:Expr> "}"
    <hi:@R> =>? {
        let global = local.is_none();
        Ok(surface::Qualifier { name, args, expr, global, span: mk_span(lo, hi) })
    }
}

Defn: surface::Defn = {
    <lo:@L> "fn" <name:Ident> "(" <args:Comma<RefineParam>> ")" "->" <sort:Sort> "{" <expr:Expr> "}" <hi:@R> => {
        surface::Defn { name, args, sort, expr, span: mk_span(lo, hi) }
    }
}

Uif: surface::UifDef = {
    <lo:@L> "fn" <name:Ident> "(" <args:Comma<RefineParam>> ")" "->" <sort:Sort> ";" <hi:@R> => {
        surface::UifDef { name, args, sort, span: mk_span(lo, hi) }
    }
}

SortDecl: surface::SortDecl = {
    "opaque" "sort" <name:Ident> ";" => {
        surface::SortDecl { name }
    }
}

pub QualNames: surface::QualNames = {
    <names:Comma<Ident>> => surface::QualNames { names }
}

RefineParam: surface::RefineParam = <name:Ident> ":" <sort:Sort> => surface::RefineParam { <> };

Sort: surface::Sort = {
    <sort: Ident> => surface::Sort::Base(sort),
    "(" <inputs:Comma<Ident>> ")" "->" <output:Ident> => surface::Sort::Func { <> },
    <input:Ident> "->" <output:Ident>                 => surface::Sort::Func { inputs: vec![input], output }
}

pub FnSig: surface::FnSig = {
    <lo:@L>
    "fn"
    <params:("<" <Comma<RefineParam>> ">")?>
    "(" <args:Args> ")"
    <returns:("->" <Ty>)?>
    <requires:("requires" <Expr>)?>
    <ensures:("ensures" <Ensures>)?>
    <hi:@R>
    => {
        let ensures = ensures.unwrap_or_default();
        let params = params.unwrap_or_default();
        surface::FnSig { params, args, returns, ensures, requires, span: mk_span(lo, hi) }
    }
}

pub Variant: surface::VariantData = {
    <lo:@L> <tys:Fields?> <ret:VariantRet> <hi:@R> => {
        let fields = match tys {
            Some(fields) => fields,
            None => vec![],
        };
        surface::VariantData { fields, ret, span: mk_span(lo, hi) }
    }
}

VariantRet: surface::VariantRet = {
    <path:Path> "[" <indices: Indices> "]" => {
        surface::VariantRet { <> }
    },
    <path:Path> <lo:@L> <hi:@R> => {
        let indices = surface::Indices { indices: vec![], span: mk_span(lo, hi) };
        surface::VariantRet { path, indices }
    }
}

Fields: Vec<surface::Ty> = {
    "(" <Comma<Ty>> ")" "->",
    "{" <Comma<Ty>> "}" "->",
}

Args    = <Comma<Arg>>;
Ensures = <Comma<(<Ident> ":" <Ty>)>>;


Arg: surface::Arg = {
    <bind:Ident> ":" "&" "strg" <ty:Ty>              => surface::Arg::StrgRef(<>),
    <bind:Ident> ":" <path:Path> "{" <pred:Expr> "}" => surface::Arg::Constr(<>),
    <bind:Ident> ":" <ty:Ty>                         => surface::Arg::Ty(Some(bind), ty),
    <ty:Ty>                                          => surface::Arg::Ty(None, <>),
}

pub Ty: surface::Ty = {
    <lo:@L> <kind:TyKind> <hi:@L> => surface::Ty {
        kind,
        span: mk_span(lo, hi)
    }
}

TyKind: surface::TyKind = {
    <bty:BaseTy>                                      => surface::TyKind::Base(<>),
    <bty:BaseTy> "[" <indices:Indices> "]"            => surface::TyKind::Indexed { <> },
    <bty:BaseTy> "{" <bind:Ident> ":" <pred:Expr> "}" => surface::TyKind::Exists { <> },
    "{" <ty:Ty> ":" <pred:Expr> "}"                   => surface::TyKind::Constr(pred, Box::new(ty)),

    "(" <tys:Comma<Ty>> ")"         => surface::TyKind::Tuple(tys),

    "&" <ty:Ty>                     => surface::TyKind::Ref(surface::RefKind::Shr, Box::new(ty)),
    "&" "mut" <ty:Ty>               => surface::TyKind::Ref(surface::RefKind::Mut, Box::new(ty)),

    "[" <ty:Ty> ";" <lo:@L> <lit:Lit> <hi:@R> "]" =>? {
        let span = mk_span(lo, hi);
        if let surface::LitKind::Integer = lit.kind && let Ok(val) = lit.symbol.as_str().parse::<usize>() {
            Ok(surface::TyKind::Array(Box::new(ty), surface::ArrayLen { val, span }))
        } else {
            Err(ParseError::User { error: UserParseError::UnexpectedToken(lo, hi) })
        }
    },
}

#[inline]
BaseTy: surface::BaseTy = {
    <lo:@L> <kind:BaseTyKind> <hi:@L> => surface::BaseTy {
        kind,
        span: mk_span(lo, hi)
    }
}

#[inline]
BaseTyKind: surface::BaseTyKind = {
    <path:Path> => surface::BaseTyKind::Path(path),
    "[" <ty:Ty> "]" => surface::BaseTyKind::Slice(Box::new(ty)),
}

GenericArgs: Vec<surface::Ty> = {
    "<" <Comma<Ty>> ">",
    "<" <Comma<Ty>> ">(?=>)",
}

RefineArgs: Vec<surface::RefineArg> = {
   "(" <Comma<RefineArg>> ")"
}

Path: surface::Path = {
    <lo:@L> <segments:Sep1<"::", Ident>> <generics:GenericArgs?> <refine:RefineArgs?> <hi:@R> => {
        surface::Path {
            segments,
            generics: generics.unwrap_or_default(),
            refine: refine.unwrap_or_default(),
            span: mk_span(lo, hi),
            res: (),
        }
    }
}

Refinement: surface::Expr = {
    <lo:@L> <lit:Lit> <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::Literal(lit),
            span: mk_span(lo, hi),
        }
    },
    <lo:@L> <var:Ident> <hi:@R> => surface::Expr {
        kind: surface::ExprKind::Var(var),
        span: mk_span(lo, hi),
    },
    "{" <Expr> "}" => <>
};

Indices: surface::Indices = {
    <lo:@L> <indices:Comma<RefineArg>> <hi:@R> => surface::Indices { indices, span: mk_span(lo, hi) }
};

RefineArg: surface::RefineArg = {
    <lo:@L> "@" <bind:Ident> <hi:@R> => surface::RefineArg::Bind(bind, surface::BindKind::At, mk_span(lo, hi)),
    <lo:@L> "#" <bind:Ident> <hi:@R> => surface::RefineArg::Bind(bind, surface::BindKind::Pound, mk_span(lo, hi)),
    <Expr>                           => surface::RefineArg::Expr(<>),
    <lo:@L> "|"<params: Comma<Ident>> "|" <body:Expr> <hi:@R> => {
        let params = params
            .into_iter()
            .map(|param| surface::RefineParam { name: param, sort: surface::Sort::Infer })
            .collect();
        surface::RefineArg::Abs(params, body, mk_span(lo, hi))
    }
};

pub Expr = Level1;

Level1 = NonAssoc<BinOp1, Level2>;  // <=>
Level2 = LeftAssoc<BinOp2, Level3>; // =>
Level3 = LeftAssoc<BinOp3, Level4>; // ||
Level4 = LeftAssoc<BinOp4, Level5>; // &&
Level5 = NonAssoc<BinOp5, Level6>;  // ==, !=, >=, <=
Level6 = LeftAssoc<BinOp6, Level7>; // >>, <<
Level7 = LeftAssoc<BinOp7, Level8>; // +, -
Level8 = LeftAssoc<BinOp8, Level9>; // *, %, /
Level9 = {
    <lo:@L> <op:UnOp> <e:Level10> <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::UnaryOp(op, Box::new(e)),
            span: mk_span(lo, hi),
        }
    },
    <Level10>
}
Level10: surface::Expr = {
    <lo:@L> "if" <p:Level1> "{" <e1:Level1> "}" "else" "{" <e2:Level1> "}" <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::IfThenElse(Box::new([p, e1, e2])),
            span: mk_span(lo, hi),
        }
    },
    <lo:@L> <lit:Lit> <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::Literal(lit),
            span: mk_span(lo, hi),
        }
    },
    <lo:@L> <var:Ident> "." <fld:Ident> <hi:@R> => {
        let expr = surface::Expr { kind: surface::ExprKind::Var(var), span: var.span};
        surface::Expr {
            kind: surface::ExprKind::Dot(var, fld),
            span: mk_span(lo, hi),
        }
    },
    <lo:@L> <f:Ident> "(" <args:Comma<Level1>> ")" <hi:@R> => {
        surface::Expr {
            kind: surface::ExprKind::App(f, args),
            span: mk_span(lo, hi),
        }
    },
    <lo:@L> <var:Ident> <hi:@R> => surface::Expr {
        kind: surface::ExprKind::Var(var),
        span: mk_span(lo, hi),
    },
    "(" <Level1> ")"
}


NonAssoc<Op, NextLevel>: surface::Expr = {
    <lo:@L> <e1:NextLevel> <op:Op> <e2:NextLevel> <hi:@R> => surface::Expr {
        kind: surface::ExprKind::BinaryOp(op, Box::new([e1, e2])),
        span: mk_span(lo, hi)
    },
    NextLevel
}
LeftAssoc<Op, NextLevel>: surface::Expr = {
    <lo:@L> <e1:LeftAssoc<Op, NextLevel>> <op:Op> <e2:NextLevel> <hi:@R> =>
        surface::Expr {
            kind: surface::ExprKind::BinaryOp(
                op,
                Box::new([e1, e2])
            ),
            span: mk_span(lo, hi),
        },
    NextLevel
}

BinOp1: surface::BinOp = {
    "<=>" => surface::BinOp::Iff
}

BinOp2: surface::BinOp = {
    "=>" => surface::BinOp::Imp
}

BinOp3: surface::BinOp = {
    "||" => surface::BinOp::Or,
}

BinOp4: surface::BinOp = {
    "&&" => surface::BinOp::And,
}

BinOp5: surface::BinOp = {
    "==" => surface::BinOp::Eq,
    "!=" => surface::BinOp::Ne,
    ">" => surface::BinOp::Gt,
    ">=" => surface::BinOp::Ge,
    "<" => surface::BinOp::Lt,
    "<=" => surface::BinOp::Le,
}

BinOp6: surface::BinOp = {
    // TODO(nilehmann) return right shift operator when we support it
    <lo:@L> ">(?=>)" ">" <hi:@R> =>? {
        Err(ParseError::User { error: UserParseError::UnexpectedToken(lo, hi) })
    }
}

BinOp7: surface::BinOp = {
    "+" => surface::BinOp::Add,
    "-" => surface::BinOp::Sub,
}

BinOp8: surface::BinOp = {
    "*" => surface::BinOp::Mul,
    "%" => surface::BinOp::Mod,
    "/" => surface::BinOp::Div,
}

UnOp: surface::UnOp = {
    "!" => surface::UnOp::Not,
    "-" => surface::UnOp::Neg,
}

Lit: surface::Lit = "literal";

Ident: surface::Ident = {
    <lo:@L> <name:"ident"> <hi:@R> => {
        surface::Ident { name, span: mk_span(lo, hi) }
    }
}

Sep<S, T>: Vec<T> = {
    <v:(<T> S)*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
}

Sep1<S, T>: Vec<T> = {
    <v:(<T> S)*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
}

Comma<T> = Sep<",", T>;

Binding<A, B>: (A, B) = <A> ":" <B>;

extern {
    type Location = Location;
    type Error = UserParseError;

    enum Token {
        "mut" => Token::Mut,
        "strg" => Token::Strg,
        "requires" => Token::Requires,
        "ensures" => Token::Ensures,
        "qualifier" => Token::Qualifier,
        "sort" => Token::Sort,
        "opaque" => Token::Opaque,
        "local" => Token::Local,
        "literal" => Token::Literal(<Lit>),
        "ident"   => Token::Ident(<Symbol>),
        "fn" => Token::Fn,
        "type" => Token::Type,
        "ref" => Token::Ref,
        "@"  => Token::At,
        "#"  => Token::Pound,
        "==" => Token::EqEq,
        "!=" => Token::Ne,
        "="  => Token::Eq,
        "&&" => Token::AndAnd,
        "||" => Token::OrOr,
        "=>" => Token::FatArrow,
        "<=>" => Token::Iff,
        "+"  => Token::Plus,
        "-"  => Token::Minus,
        "/"  => Token::Slash,
        "!"  => Token::Not,
        "*"  => Token::Star,
        "|"  => Token::Caret,
        "("  => Token::OpenDelim(Delimiter::Parenthesis),
        ")"  => Token::CloseDelim(Delimiter::Parenthesis),
        "{"  => Token::OpenDelim(Delimiter::Brace),
        "}"  => Token::CloseDelim(Delimiter::Brace),
        "["  => Token::OpenDelim(Delimiter::Bracket),
        "]"  => Token::CloseDelim(Delimiter::Bracket),
        "<"  => Token::Lt,
        "<=" => Token::Le,
        ">"  => Token::Gt,
        ">(?=>)"  => Token::GtFollowedByGt,
        ">=" => Token::Ge,
        ":"  => Token::Colon,
        "."  => Token::Dot,
        ";"  => Token::Semi,
        ","  => Token::Comma,
        "->" => Token::RArrow,
        "&"  => Token::And,
        "%"  => Token::Percent,
        "if"   => Token::If,
        "else" => Token::Else,
        "::" => Token::ModSep
    }
}
